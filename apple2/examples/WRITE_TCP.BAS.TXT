 NEW
 
 REM SEE IF EXTENSIONS HAVE ALREADY BEEN INSTALLED.  NO NEED TO 
 REM LOAD THEM MORE THAN ONCE
 10 GOSUB 10000

 REM F IS THE NETWORK ADAPTER TO OPEN
 REM 0   = NETWORK OR NETWORK_0
 REM >=1 = NETWORK_<F>

 100 PRINT "WRITE TCP"
 110 F = 0
 120 BW = -1:CN = -1:ER = -1

 130 & NSTATUS F, BW, CN, ER
 
 200 F$="N:TCP://192.168.2.100:6502"

 REM RW - 4=READ 8=WRITE 12=READ AND WRITE
 REM TR - TRANSLATION OF CR/LF 

 210 RW = 12: TR=2
 220 PRINT "OPEN ";F$
 230 & NOPEN F,RW,TR,F$

 REM BW - BYTES WAITING 
 REM CN - CONNECTED STATUS
 REM ER - ERROR FROM LAST COMMAND (SPECIFIC TO F/NETWORK ADAPTER)

 250 & NSTATUS F, BW, CN, ER
 
 REM SEND A$ OUT 10 TIMES

 300 FOR I=1 TO 10

 330 A$="FUJINET ROCKS! #"+CHR$(ASC("0")+I-1)
 
 335 PRINT "WRITE:";A$
 340 & NWRITE F,A$, LEN(A$)
 
 360 & NSTATUS F, BW, CN, ER

 490 NEXT I
 
 500 PRINT:PRINT "CLOSE"
 510 &NCLOSE F
 
 9999 END
 
REM *************************
REM DETERMINE IF FUJI EXTENSIONS ARE INSTALLED
REM *************************
10000 GOSUB 10030: IF R=1 THEN PRINT "EXTENSIONS ALREADY INSTALLED.": RETURN
10010 PRINT "LOADING EXTENSIONS..."

10020 PRINT  CHR$ (4);"BLOAD /FUJI.APPLE/FUJIAPPLE":CALL 16384:RETURN

10030 X = PEEK(1014)+PEEK(1015)*256
10040 X = X - 1
10050 C = PEEK(X)
10060 IF C = 0 OR C > 20 THEN 10140
10070 X = X - C
10080 A$=""
10090 FOR Y = 1 TO C: A$=A$+CHR$(PEEK(X)):X=X+1:NEXT Y
10100 B$ = "FUJIAMP"
10130 IF A$=B$ THEN R=1: GOTO 10150
10140 R=0
10150 RETURN

SAVE /FUJI.APPLE/WRITETCP.BAS

10020 PRINT  CHR$ (4);"BLOAD FUJIAPPLE":CALL 16384:RETURN

SAVE WRITETCP.BAS


