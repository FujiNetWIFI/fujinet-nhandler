TEXT:HOME

NEW 

3 DIM F(1),S(1,9),LO%(79,47), AX%(79): TR=1

REM                     ATARI CR 
5 GOSUB 10000 : N0=0 : &NSETEOL 155

REM BL=BACKGROUND LIGHT   BD=BACKGROUND DARK
6 TEXT:HOME: OX=16:OY=0:SS=3:BL=12:BD=4:CU=6:TH=15:ME=0:GOSUB 650:HOME

10 GOTO 500

REM SLOW DRAW BOARD

11 FOR Y=0 TO 39:FOR X=OX TO 79:IF AX%(X) > -1 THEN COLOR=LO%(X,Y):PLOT AX%(X),Y
12 NEXT:NEXT:RETURN

REM QUICK BLANK BOARD

13 COLOR=BL:FOR Y=0 TO 39:HLIN 0,39 AT Y:NEXT:COLOR=BD:FOR Y=1 TO 2: HLIN 1,38 AT Y:HLIN 1,38 AT Y+35:NEXT
14 FOR Y=3 TO 35 STEP 4: HLIN 1, 38 AT Y: NEXT
15 FOR X=1 TO 4: VLIN 3,34 AT X:NEXT:FOR X=36 TO 38:VLIN 3,34 AT X:NEXT
16 FOR X=8 TO 34 STEP 4:VLIN 2,34 AT X:NEXT
17 RETURN 

REM SET the color at x,y and x+1,y equal to what's at x,y-1

18 C = LO%(X,Y-1):LO%(X,Y)=C:LO%(X+1,Y)=C:I=X:J=Y:GOSUB 100:I=X+1:GOSUB 100:RETURN

REM GET THE COLOR AT X-1,Y-1 (X,Y) MAY HAVE A FLASHING CURSOR

19 C=LO%(X-1,Y-1):GOTO 30

REM ***************************************************************************************
REM ***************************************************************************************
REM ***************************************************************************************

REM *************************************
REM I THINK THIS IS THE FLASHING CURSOR
REM GET COLOR AT X-1, Y, 
REM IF COLOR IS THE SAME AS C, MAKE THE C=0 OTHERWISE NO CHANGE
REM USING THAT COLOR SET X,Y AND X+1,Y TO C
REM *************************************

20 CC = LO%( X-1,Y):IF C=CC THEN C=BL

30 LO%(X,Y)=C:LO%(X+1,Y)=C:I=X:J=Y:GOSUB 100:I=X+1:GOSUB 100:GOTO 60

REM SEE IF THE COLOR AT X-1,Y IS THE SAME AS C
REM IF NOT, MAKE IT THE SAME

REM SET A COLOR OF A SQUARE

40 Z = LO%(X-1,Y):IF Z=C THEN RETURN

45 COLOR = C:FOR I=X-1 TO X+2:FOR J=Y-1 TO Y+1
50 LO%(I,J)=C:GOSUB 100:NEXT J:NEXT I

REM ***************************************
REM SEE IF THERE IS INPUT FROM REMOTE SITE
REM ***************************************

60 TRIG=1:& NSTATUS N0, BW, CN, ERR:IF BW>0 THEN GOSUB 18: & NINPUT N0, K$:GOSUB 240 

REM **** SERIOUS BUG HERE -- RETURN IS LOST EVERTIME TRIGGER IS PRESSED (TRIG=0)
65 IF TRIG = 0 THEN POP: GOTO 1700
70 RETURN

REM SET COLOR BASED ON PLAYER

90 C=TH:IF P=1 THEN C=ME
95 RETURN

REM ***************************************************************************************
REM ***************************************************************************************
REM ***************************************************************************************

REM PLOT AT I,J

100 C = LO%(I,J):COLOR=C:IF AX%(I) > -1 AND J<40 THEN PLOT AX%(I),J
110 RETURN

REM ***************************************************
REM THIS ROUTINE REMOVES CONTENT BEFORE THE FIRST COMMA
REM FROM A STRING
REM ***************************************************

200 FC=1
210 IF MID$(K$, FC, 1) = "," THEN K$= RIGHT$(K$, LEN(K$)-FC) : RETURN
220 FC = FC + 1: IF FC < LEN(K$) THEN 210
230 RETURN

REM ***************************************************
REM THIS TAKES 3 COMMA SEPARATED VALUES IN K$ AND RETURNS THEM AS 
REM X,Y,TRIG
REM ***************************************************

240 X = VAL(K$):GOSUB 200:Y=VAL(K$):GOSUB 200: TRIG=VAL(K$)
250 RETURN

REM CENTER TEXT ON A 40 COLUMN DISPLAY
300 M=LEN(K$)/2:HTAB 20-M: PRINT K$;: RETURN

REM ********************************************************
REM ********************************************************
REM START OF PROGRAM
REM ********************************************************
REM ********************************************************

REM LO IS THE ATARI REPRESENTATION OF THE SCREEN
REM AX CONVERT ATARI X COORDINATE TO APPLE X COORDINATE

500 LS=0

530 VTAB 10: K$= "#FUJINET REVERSI - APPLE EDITION": INVERSE:GOSUB 300:NORMAL

531 VTAB 15: HTAB 1: K$ = "BE SURE TO FORWARD TCP PORT 6502.":GOSUB 300: PRINT
540 VTAB 20: PRINT "TYPE HOSTNAME OR PRESS ";:INVERSE: PRINT "ENTER";: NORMAL: PRINT " TO HOST"
550 INPUT H$
560 IF H$="" THEN LS=1
565 HOME
570 D$="N:TCP://"+H$+":6502/": PRINT "OPENING ";D$

571 & NOPEN N0,12,0,D$
572 IF NOT LS THEN 600

575 PRINT "WAITING FOR CONNECTION..."
576 W$="/-\-|-":W=1
580 & NSTATUS N0,BW,CN,ER
582 PRINT MID$(W$,W,1);CHR$(8);
583 W=W+1:IF W>6 THEN W=1
585 IF CN = 0 THEN 580

590 PRINT "ACCEPTED CONNECTION.": &NACCEPT N0

600 GC=0:HOME:HTAB 5: VTAB 21:PRINT "TYPE NAME AND THEN ";:INVERSE: PRINT "RETURN":NORMAL
601 IF NOT LS THEN 620

610 P=1:PRINT:PRINT "PLAYER 1";:INPUT A$:IF A$="" THEN 610
615 & NPRINT N0, A$
616 & NSTATUS N0,BW,CN,ER:IF BW = 0 THEN 616
617 & NINPUT N0, B$

618 GOTO 700

620 P=2:PRINT:PRINT "PLAYER 2";:INPUT B$:IF B$="" THEN 620
622 & NPRINT N0, B$
623 & NSTATUS N0,BW,CN,ER:IF BW = 0 THEN 623
624 & NINPUT N0, A$

625 GOTO 700


REM ******************************************************************
REM AX - ATARI TO APPLE TRANSLATION
REM ******************************************************************

650 PRINT "ONE TIME TABLE SETUP": PRINT "CONVERTING ATARI TO APPLE COORDINATES..."

REM                                     DON'T START SKIP UNTIL COLUMN 20
REM SLOW
655 X1=0:X2=0:FOR X=OX TO 79:AX%(X)=-1:IF X < 20 THEN 675

REM *** SPEED UP
REM SET EVERYTHING TO -1 WITH NLET
655 &NLET AX%, -1:X1=0:X2=0:FOR X=OX TO 79:IF X < 20 THEN 675
660 IF X2 > 39 THEN 680

REM SKIP EVERY 3RD COLUMN
REM *** SLOW
665 X1=X1+1:IF X1=SS THEN AX%(X)=-1:GOTO 680

REM *** SPEED UP
665 X1=X1+1:IF X1=SS THEN 680

REM START COUNTING AGAIN AFTER #5
670 IF X1 = 5 THEN X1=0
675 AX%(X) = X2 : X2=X2+1
 
680 NEXT:RETURN

REM ***********************************************************************
REM ***********************************************************************
REM BUILD UP THE BOARD
REM ***********************************************************************
REM ***********************************************************************
700 HOME:INVERSE:PRINT "PLEASE WAIT...":NORMAL:C=BL 

REM FORCE VISIBLE AREA TO BE LIGHT GREEN
710 FOR Y=0 TO 39:FOR X=OX TO 67:LO%(X,Y)=BL:NEXT:NEXT

REM **** USE OUR NEW NLET ROUTINE
710 &NLET LO%,BL

750 C=BD

775 GR:GOSUB 13
REM ***********************************************************************
REM LINES DOWN
REM ***********************************************************************
REM 760 FOR Y=1 TO 37:LO%(17,Y)=C:LO%(18,Y)=C:LO%(19,Y)=C:FOR X=20 TO 60 STEP 5:LO%(X,Y)=C:NEXT X:LO%(61,Y)=C:LO%( 62,Y)=C:LO%(63,Y)=C:NEXT Y

REM ***********************************************************************
REM LINES ACROSS
REM ***********************************************************************
REM 800 FOR X=17 TO 60:LO%(X,1)=C:LO%(X,2)=C:FOR Y=3 TO 35 STEP 4:LO%(X,Y)=C:NEXT Y:LO%(X,36)=C:LO%(X,37)=C: NEXT X

REM STARTING BLOCKS
850 FOR L=1 TO 6:READ X,Y,C:GOSUB 40:NEXT L:RESTORE
REM            ME      ME      ME      TH       TH       TH
855 DATA 10,36,0,37,17,0,42,21,0,72,36,15,42,17,15,37,21,15

REM DRAW THE SCREEN - ALBEIT SLOWLY
REM 860 GOSUB 11

REM ***********************************************************************
REM ***********************************************************************
REM ***********************************************************************

REM ***********************************************************************
REM CALCULATE SCORE
REM ***********************************************************************


990 P=2

REM THIS IS A PRETTY CLEVER WAY TO SWITCH P BETWEEN 1 AND 2
REM  FIRST TIME P=1   P = 3-2 
REM SECOND TIME P=2   P = 3-1 

REM SWITCH PLAYERS BETWEEN P=1 AND P=2
REM THERE ARE GOTO'S TO HERE FROM 1150, 1630, 1780
1000 P=3-P

REM A= num of player 1 pieces B= num of player 2 pieces

1010 A=0:B=0:FOR M=22 TO 57 STEP 5:FOR N=5 TO 33 STEP 4:C = LO%( M,N ):IF C=ME THEN A=A+1
1030 IF C=TH THEN B=B+1
1040 IF C=BL THEN X=M:Y=N
1050 NEXT:NEXT

1055 VTAB 21:HTAB 5 : NORMAL:PRINT A$; : HTAB 35-LEN(B$):INVERSE:PRINT B$;:NORMAL: VTAB 22:HTAB LEN(A$)/2 + 5 : PRINT A;" ";: HTAB 35-LEN(B$)/2:PRINT B;" ";

REM Game over

1070 IF A+B=64 THEN 2000

REM LOOKS LIKE THIS IS ALWAYS ZERO ???
1110 IF F(P-1)=0 THEN 1500

1120 GOSUB 5000
1130 IF Q>0 THEN 1730
1140 FOR L=0 TO 20
1143   PRINT "I PASS";:GOSUB 60:PRINT " ";
1145 NEXT

REM ADJUST P
1150 GOTO 1000 


REM **************
REM KEYBOARD INPUT
REM **************

1500 GOSUB 90
1505 GOSUB 20:GOSUB 20:GOSUB 19
1510 N=255:TRIG=1:JS=15:K=0:IF PEEK(-16384) > 128 THEN GET K$: K=ASC(K$)

REM IGNORE KEYPRESSES WHEN IT'S NOT OUR TURN

1511 IF LS=0 AND P=1 THEN 1500
1512 IF LS=1 AND P=2 THEN 1500

REM CONVERT FROM APPLE KEYS TO ATARI

1514 IF K=32 THEN N=33 : REM SPACE
1515 IF K=13 THEN N=12 : REM ENTER
1516 IF K=49 THEN N=31 : REM '1'
1517 IF K=50 THEN N=30 : REM '2'
REM        RETURN    TAB 
REM 1516 IF K=76 OR K=108 THEN N=10 : REM INVALID MOVE ????
1518 IF K=76 OR K=21 THEN N=7  : REM 'K' RIGHT ARROW
1519 IF K=74 OR K=8  THEN N=6  : REM 'J' LEFT ARROW
1520 IF K=75 OR K=10 THEN N=15 : REM 'L' DOWN ARROW
1521 IF K=73 OR K=11 THEN N=14 : REM 'I' UP ARROW

REM NO MOVEMENT, IGNORE
1525 IF N=255 AND TRIG=1 AND JS=15 THEN 1500
1530 IF N=33 OR N=12 OR TRIG=0 THEN 1698

REM  if press '0' then color = 

1540 IF N=30 THEN C=TH:GOSUB 40:GOTO 1500

1550 IF N=31 THEN C=ME:GOSUB 40:GOTO 1500

1560 IF N=10 THEN 1630

REM KEYBOARD (N)           JOYSTICK
REM    (14) I                  14 
REM (6) J      L (7)      11   15    7
REM    (15) K                  13  

REM   RIGHT                             LEFT
1570 X=X+5*((N=7 OR JS=7) AND X<57)-5*((N=6 OR JS=11) AND X>22)

REM   DOWN                              UP
1580 Y=Y+4*((N=15 OR JS=13) AND Y<33)-4*((N=14 OR JS=14) AND Y>5)

REM When trigger is pressed TRIG=0
1581 IF N=33 OR N=12 THEN TRIG=0  
1582 O$ = STR$(X)+","+STR$(Y)+","+STR$(TRIG):& NPRINT N0, O$
1590 GOTO 1500


1630 GOSUB 19:GOSUB 5000:IF Q=0 THEN 1000
1655 FOR I=0 TO 20:NORMAL:IF I/2 = INT(I/2) THEN INVERSE
1656    VTAB 21:K$="INVALID MOVE":GOSUB 300:GOSUB 60
1657 NEXT I:VTAB 21:NORMAL:K$= "            ":GOSUB 300:GOTO 1010

REM When trigger is pressed TRIG=0
1698 IF N=33 OR N=12 THEN TRIG=0
1699 O$ = STR$(X)+","+STR$(Y)+","+STR$(TRIG):& NPRINT N0, O$

REM IF R = 0 THEN INVALID MOVE
1700 G=0:GOSUB 4000:IF R=0 THEN GOSUB 19:GOTO 1655
1730 G=1:GOSUB 4000
1780 GOTO 1000

REM END OF GAME

2000 VTAB 22: K$="TO SEE SCOREBOARD, PRESS ANY KEY":GOSUB 300:GET K$
2040 S(0,GC)=A:S(1,GC)=B
2050 TEXT:HOME:HTAB 1:VTAB 10:PRINT "GAME":HTAB 1: VTAB 12:PRINT A$;:HTAB  1:VTAB 14:PRINT B$;
2060 FOR X=0 TO GC
2061   N=X*3+10:HTAB N: VTAB 10:PRINT X+1;:HTAB N: VTAB 12:PRINT S(0,X);:HTAB N: VTAB 14:PRINT S(1,X);
2062 NEXT X
2100 PRINT :PRINT :PRINT "SAME PLAYERS AGAIN? (Y-N)";:GET K$:N=(K$="Y" OR K$="y")*89 + (K$="N" OR K$="n")*78
2120 IF GC>7 AND N=89 THEN PRINT :PRINT "YOU HAVE ALREADY PLAYED 9 GAMES.";:FOR X=1 TO 999:NEXT X:GOTO 600
2130 IF N=89 THEN GC=GC+1:GOTO 700
2135 IF N<>78 THEN 2100
2140 GOTO 600
3000 TEXT:HOME:PRINT "DISCONNECTED.":& NCLOSE 0:END

REM *********************************
REM G=0  DETERMINE IF VALID MOVE
REM G=1  FLIP TO OUR COLOR
REM *********************************

4000 R=0:SX=X:SY=Y
REM ONLY THE BACKGROUND COLOR IS VALID LOCATION
4005 C=LO%(X-1,Y):IF C<>BL THEN RETURN

4010 FOR DX=-1 TO 1:FOR DY=-1 TO 1
4020 IF DX=0 AND DY=0 THEN 4130
4030 TR=0:X=SX:Y=SY
4050 X=X+DX*5:Y=Y+DY*4:C = LO%(X,Y)

REM 4060 IF C=3-P THEN TR=TR+1:GOTO 4050
REM 4070 IF C=P THEN R=R+TR:IF G=1 THEN 4100
4055 IF P=2 THEN 4075
4060 IF C=TH THEN TR=TR+1:GOTO 4050
4070 IF C=ME THEN R=R+TR:IF G=1 THEN 4100
4073 GOTO 4130
4075 IF C=ME THEN TR=TR+1:GOTO 4050
4077 IF C=TH THEN R=R+TR:IF G=1 THEN 4100
4080 GOTO 4130

4100 X=X-DX*5:Y=Y-DY*4
REM SET COLOR, DRAW BLOCKS
4105 GOSUB 90 : GOSUB 40
4110 IF NOT (X=SX AND Y=SY) THEN 4100

4130 NEXT DY:NEXT DX:X=SX:Y=SY:RETURN


REM CONVERT SQUARES TO OUR COLOR

5000 Q=0:QX=0:QY=0
5010 FOR M=0 TO 7:FOR N=0 TO 7:X=M*5+22:Y=N*4+5:C = LO%( X,Y ):IF C<>BL THEN 5090
5033 GOSUB 90
5035 GOSUB 20:GOSUB 19:G=0:GOSUB 4000:IF R>Q THEN Q=R:QX=X:QY=Y
5090 NEXT N:NEXT M:X=QX:Y=QY:RETURN

REM JUNK CODE???

7005 FOR X=0 TO 8:S(0,X)=0:S(1,X)=0:NEXT X
8000 GOTO 8000



REM *************************
REM DETERMINE IF FUJI EXTENSIONS ARE INSTALLED
REM *************************
10000 GOSUB 10030: IF R=1 THEN PRINT "EXTENSIONS ALREADY INSTALLED.": RETURN
10010 PRINT "LOADING EXTENSIONS..."

10020 PRINT  CHR$ (4);"BLOAD /FUJI.APPLE/FUJIAPPLE":CALL 16384:RETURN

10030 X = PEEK(1014)+PEEK(1015)*256
10040 X = X - 1
10050 C = PEEK(X)
10060 IF C = 0 OR C > 20 THEN 10140
10070 X = X - C
10080 A$=""
10090 FOR Y = 1 TO C: A$=A$+CHR$(PEEK(X)):X=X+1:NEXT Y
10100 B$ = "FUJIAMP"
10130 IF A$=B$ THEN R=1: GOTO 10150
10140 R=0
10150 RETURN

SAVE /FUJI.APPLE/REVERSI.BAS

10020 PRINT  CHR$ (4);"BLOAD FUJIAPPLE":CALL 16384:RETURN

SAVE REVERSI.BAS

